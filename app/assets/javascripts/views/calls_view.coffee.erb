CincyService.CallsView = Ember.View.extend
  map: null
  markers: null

  init: ->
    @markerMap = {}
    @_super()

  didInsertElement: ->
    @drawMap()

  drawMap: ->
    center = [39.09676, -84.51387]
    controller = @get('controller')
    zoomLevel = controller.get('zoomLevel')
    @set('map', L.map('map'))
    @set('markers', L.markerClusterGroup(maxClusterRadius: 30))
    map = @get('map')
    map.setView(center, zoomLevel)

    L.tileLayer('http://{s}.tile.openstreetmap.fr/hot/{z}/{x}/{y}.png', {
      attribution: '&copy; <a href="http://openstreetmap.org">OpenStreetMap</a> contributors, <a href="http://creativecommons.org/licenses/by-sa/2.0/">CC-BY-SA</a>, Tiles courtesy of <a href="http://hot.openstreetmap.org/" target="_blank">Humanitarian OpenStreetMap Team</a>'
    }).addTo(map)

    map.on 'moveend', (e) ->
      controller.set('zoomLevel', e.target.getZoom())
      controller.set('mapBounds', e.target.getBounds())

    @controller.set('map', map)

  contentDidChange: (->
    map = @get('map')
    return unless map

    @get('controller').forEach (item, index) =>
      return if item.get('isMapped')
      marker = L.marker([item.get('latitude'), item.get('longitude')], icon: @markerIcon())
      marker._popup = @markerContent(item)
      marker.on 'click', (e) =>
        @get('controller').transitionToRoute('call', item)
      item.set('isMapped', true)
      @get('markers').addLayer(marker)
      @markerMap[item.get('id')] = marker
    map.addLayer(@get('markers'))
  ).observes('controller.content.length')

  markerIcon: ->
    new L.Icon
      iconUrl: '<%= asset_path("leaflet-0.7.1/images/marker-icon.png") %>'
      iconRetinaUrl: '<%= asset_path("leaflet-0.7.1/images/marker-icon-2x.png") %>'
      shadowUrl: '<%= asset_path("leaflet-0.7.1/images/marker-shadow.png") %>'
      shadowRetinaUrl: '<%= asset_path("leaflet-0.7.1/images/marker-shadow.png") %>'

  markerContent: (call) ->
    L.popup().setContent(
      "<h4 style='font-size: 120%; font-weight: bold;'>#{call.get('srType')}</h4>
       <h5 style='font-size: 80%'>#{call.get('csrNumber')} | #{call.get('status')} | #{call.get('address')}</h5>
       <p style='font-size: 90%; margin: 8px 0;'>#{call.get('description')}</p>
       <h5 style='font-size: 80%'>#{call.get('receivedAt')}</h5>")

  selectedCallDidChange: (->
    selectedCall = @controller.get('selectedCall')
    marker = @markerMap[selectedCall?.get('id')]
    marker?.openPopup()
  ).observes('controller.selectedCall')

